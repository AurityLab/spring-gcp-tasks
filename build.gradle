plugins {
    // id("org.springframework.boot") version "2.1.6.RELEASE"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"

    id "org.jetbrains.kotlin.jvm" version "1.3.40"
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.40"
}

group = "com.auritylab.spring.gcp.tasks"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
}

ext {
    set('springCloudVersion', "Greenwich.SR1")
}

configurations {
    ktlint
}

dependencies {
    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.9"

    // spring
    implementation "org.springframework:spring-web:5.1.8.RELEASE"
    implementation "org.springframework.cloud:spring-cloud-gcp-starter"

    // gcp standalone
    implementation "com.google.api-client:google-api-client:1.30.1"
    implementation "com.google.apis:google-api-services-cloudtasks:v2beta3-rev19-1.25.0"

    // ktlint
    ktlint("com.pinterest:ktlint:0.33.0")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

compileKotlin {
    kotlinOptions.freeCompilerArgs = Collections.singletonList("-Xjsr305=strict")
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.freeCompilerArgs = Collections.singletonList("-Xjsr305=strict")
    kotlinOptions.jvmTarget = "1.8"
}
