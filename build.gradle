plugins {
    id "io.spring.dependency-management" version "1.0.8.RELEASE"

    id "org.jetbrains.kotlin.jvm" version "1.3.41"
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.41"

    id "org.gradle.maven-publish"
    id "nu.studer.credentials" version "1.0.7"
}

group "com.auritylab.spring.gcp.tasks"
version "0.1.5-SNAPSHOT"
sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
}

ext {
    set('springCloudVersion', "Greenwich.SR2")
}

configurations {
    ktlint
}

dependencies {
    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.9"

    // spring
    implementation "org.springframework:spring-core:5.1.9.RELEASE"
    implementation "org.springframework:spring-web:5.1.9.RELEASE"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:2.1.6.RELEASE"

    // spring cloud
    implementation "org.springframework.cloud:spring-cloud-gcp-starter"

    // gcp
    implementation "com.google.cloud:google-cloud-tasks"

    // gson
    implementation "com.google.code.gson:gson:2.8.5"

    // ktlint
    ktlint("com.pinterest:ktlint:0.34.2")

    // spring for tests
    testImplementation "org.springframework:spring-test:5.1.9.RELEASE"

    // tomcat for tests
    testImplementation "javax.annotation:javax.annotation-api:1.3.2"
    testImplementation("org.apache.tomcat.embed:tomcat-embed-core:9.0.22") {
        exclude group: "tomcat-annotations-api", module: "org.apache.tomcat"
    }
    testImplementation "org.apache.tomcat.embed:tomcat-embed-el:9.0.22"
    testImplementation "org.apache.tomcat.embed:tomcat-embed-websocket:9.0.22"

    // JUnit
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.5.1"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.5.1"

    // Mockito
    testImplementation "org.mockito:mockito-core:3.0.0"
}
compileKotlin.dependsOn(processResources)
compileJava.dependsOn(processResources)

dependencyManagement {
    imports {
        mavenBom("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}") {
            bomProperty "google-cloud-bom.version", "0.99.0-alpha"
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "http://nexus.auritylab.com:8081/repository/maven-private/"
            credentials {
                username project.credentials.getProperty("auritylab.nexus.username").toString()
                password project.credentials.getProperty("auritylab.nexus.password").toString()
            }
        }
    }
}

test {
    useJUnitPlatform()

    dependsOn 'cleanTest'

    testLogging {
        events "passed", "skipped", "failed"
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint
test.dependsOn ktlint

compileKotlin {
    kotlinOptions.freeCompilerArgs = Collections.singletonList("-Xjsr305=strict")
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.freeCompilerArgs = Collections.singletonList("-Xjsr305=strict")
    kotlinOptions.jvmTarget = "1.8"
}
