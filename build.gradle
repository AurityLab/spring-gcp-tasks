plugins {
    id "io.spring.dependency-management" version "1.0.9.RELEASE"

    id "org.jetbrains.kotlin.jvm" version "1.3.71"
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.71"
    id "org.jetbrains.kotlin.kapt" version "1.3.70"

    id "org.gradle.maven-publish"
    id "nu.studer.credentials" version "2.1"
}

group "com.auritylab"
version "0.1.13"
sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
}

ext {
    set("springCloudVersion", "Hoxton.SR1")
}

configurations {
    ktlint
}

dependencies {
    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.10.3"

    // spring
    implementation "org.springframework:spring-core:5.2.5.RELEASE"
    implementation "org.springframework:spring-web:5.2.4.RELEASE"
    kapt "org.springframework.boot:spring-boot-configuration-processor:2.2.5.RELEASE"

    // spring cloud
    implementation "org.springframework.cloud:spring-cloud-gcp-starter"

    // gcp
    implementation "com.google.cloud:google-cloud-tasks"

    // ktlint
    ktlint("com.pinterest:ktlint:0.36.0")

    // spring for tests
    testImplementation "org.springframework:spring-test:5.2.5.RELEASE"

    // tomcat for tests
    testImplementation "javax.annotation:javax.annotation-api:1.3.2"
    testImplementation("org.apache.tomcat.embed:tomcat-embed-core:9.0.33") {
        exclude group: "tomcat-annotations-api", module: "org.apache.tomcat"
    }
    testImplementation "org.apache.tomcat.embed:tomcat-embed-el:9.0.33"
    testImplementation "org.apache.tomcat.embed:tomcat-embed-websocket:9.0.33"

    // JUnit
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.1"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.6.1"

    // Mockito
    testImplementation "org.mockito:mockito-core:3.3.3"

    // Gson
    testImplementation "com.google.code.gson:gson:2.8.6"
}
compileKotlin.dependsOn(processResources)
compileJava.dependsOn(processResources)

dependencyManagement {
    imports {
        mavenBom("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")
    }
}

publishing {
    repositories {
        maven {
            url "https://maven.pkg.github.com/AurityLab/spring-gcp-tasks"
            credentials {
                username project.credentials.getProperty("auritylab.github.username").toString()
                password project.credentials.getProperty("auritylab.github.password").toString()
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

test {
    useJUnitPlatform()

    dependsOn "cleanTest"

    testLogging {
        events "passed", "skipped", "failed"
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint
test.dependsOn ktlint

compileKotlin {
    kotlinOptions.freeCompilerArgs = Collections.singletonList("-Xjsr305=strict")
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.freeCompilerArgs = Collections.singletonList("-Xjsr305=strict")
    kotlinOptions.jvmTarget = "1.8"
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
